//@version=3
//ADXDMI Refurb.
study("ADX and DI")
len = input(title="Length", type=integer, defval=14)
th = input(title="threshold", type=integer, defval=20)
exzone = input(title="exhaustion zone start", type=integer, defval=45)

TrueRange = max(max(high-low, abs(high-nz(close[1]))), abs(low-nz(close[1])))
DirectionalMovementPlus = high-nz(high[1]) > nz(low[1])-low ? max(high-nz(high[1]), 0): 0
DirectionalMovementMinus = nz(low[1])-low > high-nz(high[1]) ? max(nz(low[1])-low, 0): 0

SmoothedTrueRange = nz(SmoothedTrueRange[1]) - (nz(SmoothedTrueRange[1])/len) + TrueRange
SmoothedDirectionalMovementPlus = nz(SmoothedDirectionalMovementPlus[1]) - (nz(SmoothedDirectionalMovementPlus[1])/len) + DirectionalMovementPlus
SmoothedDirectionalMovementMinus = nz(SmoothedDirectionalMovementMinus[1]) - (nz(SmoothedDirectionalMovementMinus[1])/len) + DirectionalMovementMinus

DIPlus = SmoothedDirectionalMovementPlus / SmoothedTrueRange * 100
DIMinus = SmoothedDirectionalMovementMinus / SmoothedTrueRange * 100
DX = abs(DIPlus-DIMinus) / (DIPlus+DIMinus)*100
ADX = sma(DX, len)

cColor = (ADX < th) ? gray : (DIPlus > DIMinus) ? (ADX>exzone ? green : lime) : (ADX>exzone ? maroon : red)
top = ADX < ADX[1] and ADX[2] < ADX[1] and ADX[3] < ADX[2] and ADX > exzone

plot(ADX, color=cColor, title="ADX", style=columns)
plotchar(top ? (ADX+2) : na, char="*", color=cColor, size=size.small, location=location.absolute,offset=-1)